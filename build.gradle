/*
 * Copyright (C) 2021 Emerson Pinter - All Rights Reserved
 */

group 'br.com.pinter.tqrespec'
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'org.sonarqube'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.1.1"
    }
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

sourceSets {
    main.output.resourcesDir = main.java.outputDir
}


OperatingSystem OS = DefaultNativePlatform.currentOperatingSystem
ext.distAppName = 'TQRespec'
version = '0.10.0'
sourceCompatibility = 14
ext.moduleName = 'br.com.pinter.tqrespec'
ext.mainPackageName = 'br.com.pinter.tqrespec'
ext.javahome = System.properties['java.home']
ext.moduleInfoPresent = false
ext.osArch = System.getProperty("os.arch").toLowerCase()
ext.javafxSdkDir = file(String.format("%s/sdk/javafx-sdk-%s-%s-%s", projectDir, sourceCompatibility, OS.toFamilyName().toLowerCase(), osArch))
ext.javafxSdkBinDir = file("${javafxSdkDir}/bin")
ext.javafxSdkLibDir = file("${javafxSdkDir}/lib")
ext.patchjarDir = new File(buildDir, '/patchedjar')
ext.patchJars = false
apply from: "gradle/patchModulesJar.gradle"
if (file('src/main/java/module-info.java').exists()) {
    ext.moduleInfoPresent = true
}
mainClassName = "br.com.pinter.tqrespec.Main"

if (!file(javafxSdkDir).directory) {
    throw new GradleException(String.format("ERROR: openjfx-sdk for java %s (%s) not found in %s", sourceCompatibility, System.getProperty("os.arch"), javafxSdkDir))
}

if (project.hasProperty('buildVersion')) {
    project.version = project.properties['buildVersion']
}
if(osArch.equals("amd64")) {
    ext.appArchiveName = "${distAppName}-${project.version}.zip"
} else {
    ext.appArchiveName = "${distAppName}-${project.version}_${osArch}.zip"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation fileTree(dir: javafxSdkLibDir, include: '*.jar')
    implementation group: 'com.google.inject', name: 'guice', version: '5.0.1'
    implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.3.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.3'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'

    implementation project(':tqdatabase')

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
    testImplementation 'org.mockito:mockito-core:3.9.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.9.0'
    testImplementation group: 'com.google.inject', name: 'guice', version: '5.0.1'
}

test {
    useJUnitPlatform()

    jvmArgs '--add-opens','java.base/java.lang=ALL-UNNAMED',
            '--add-opens','java.base/java.util.concurrent=ALL-UNNAMED',
            '--add-opens','java.base/java.io=ALL-UNNAMED',
            '--add-opens','java.base/java.nio=ALL-UNNAMED',
            '--add-opens','java.base/java.nio.file=ALL-UNNAMED',
            '--add-opens','java.logging/java.util.logging=ALL-UNNAMED'
}

if (OS.isWindows()) {
    ext.binjlink = javahome + "/bin/jlink.exe"
    ext.binjpackage = javahome + "/bin/jpackage.exe"
} else {
    ext.binjlink = javahome + "/bin/jlink"
    ext.binjpackage = javahome + "/bin/jpackage"
}

jar {
    manifest {
        attributes('Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                'Implementation-Title': distAppName,
                'Implementation-Version': project.version,
                'Automatic-Module-Name': moduleName,
                'Main-Class': mainClassName)
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task copyMetaInf(type: Copy) {
    from fileTree(new File(sourceSets.main.output.resourcesDir, '/META-INF'))
    into new File(sourceSets.main.java.outputDir, '/META-INF')
}

if (moduleInfoPresent) {
    compileJava {
        inputs.property("moduleName", moduleName)
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
                    '--patch-module', "$moduleName=" + files(sourceSets.main.resources.srcDirs).asPath,
            ]
            classpath = files()
        }
    }

    run {
        inputs.property("moduleName", moduleName)
        doFirst {
            jvmArgs = [
                    '-XX:+UseSerialGC',
                    '-Dguice_bytecode_gen_option=DISABLED',
                    '-XX:MinHeapFreeRatio=10', '-XX:MaxHeapFreeRatio=15',
                    '--add-reads', "${moduleName}=ALL-UNNAMED",
                    '--add-opens', 'javafx.base/com.sun.javafx.reflect=ALL-UNNAMED',
                    '--add-opens', "${moduleName}/${mainPackageName}=ALL-UNNAMED",
                    '--add-opens', "${moduleName}/${mainPackageName}.core=ALL-UNNAMED",
                    '--add-opens', "${moduleName}/${mainPackageName}.tqdata=ALL-UNNAMED",
                    '--add-opens', "${moduleName}/${mainPackageName}.gui=ALL-UNNAMED",
                    '--add-opens', "${moduleName}/${mainPackageName}.save.player=ALL-UNNAMED",
                    '--add-opens', "${moduleName}/${mainPackageName}.save.stash=ALL-UNNAMED",
                    '--add-opens', "${moduleName}/${mainPackageName}.save=ALL-UNNAMED",
                    '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
                    '--module-path', classpath.asPath,
                    '--class-path', classpath.asPath,
                    '--module', moduleName,
                    '--patch-module', "$moduleName=" + files(sourceSets.main.output.resourcesDir).asPath,
            ]
            classpath = files()
        }
    }
} else {
    run {
        doFirst {
            jvmArgs = [
                    '-XX:+UseSerialGC',
                    '-XX:MinHeapFreeRatio=10', '-XX:MaxHeapFreeRatio=15',
                    '--module-path', "${javafxSdkLibDir}",
                    '--add-modules', 'javafx.base,javafx.fxml,javafx.graphics,javafx.controls',
            ]
        }
    }
}

task copyToLib(type: Copy) {
    doFirst {
        mkdir "$buildDir/jardist"
    }
    from(configurations.runtimeClasspath.findAll { !it.getName().toLowerCase().matches("javafx.*\\.jar") }).collect { it.getPath() }

    from jar
    into "$buildDir/jardist"
}

copyToLib.dependsOn(jar)
classes.dependsOn('copyMetaInf')

task copyJarsToModuleDeps(type: Copy) {
    from project(':tqdatabase').configurations.compileClasspath
    from project.configurations.compileClasspath
    into patchjarDir
}

task patchModulesJar(type: PatchJarModuleTask) {
    dependsOn 'copyJarsToModuleDeps'
    delete = true
    tempPatchDir = new File(buildDir, '/temppatch')
    tempJarDir = patchjarDir
    moduleDeps = ['com.sun.jna.platform': 'com.sun.jna']
}

task copyAppimageFiles(type: Copy) {
    dependsOn(jar)
    dependsOn 'tqdatabase:jar'
    doFirst {
        mkdir "$buildDir/appimage-files"
    }
    if (!moduleInfoPresent) {
        from project(':tqdatabase').tasks.jar.outputs.files.collect { it.getPath() }
        from jar
    }
    from(configurations.runtimeClasspath.findAll {
        !it.getName().toLowerCase().matches("javafx.*.jar") && !it.getName().toLowerCase().matches("tqdatabase.*.jar")
    }).collect { it.getPath() }
    into "$buildDir/appimage-files"
}

task copyAppimageModules(type: Copy) {
    if (patchJars) {
        dependsOn(patchModulesJar)
    }

    dependsOn(jar)
    dependsOn 'tqdatabase:jar'
    doFirst {
        mkdir "$buildDir/appimage-modules"
    }
    from project(':tqdatabase').tasks.jar.outputs.files.collect { it.getPath() }
    if (moduleInfoPresent) {
        if (patchJars) {
            from patchjarDir
        }
        from jar
    }
    into "$buildDir/appimage-modules"
}

task jlink(type: Exec) {
    dependsOn clean
    dependsOn 'copyToLib'
    if (patchJars) {
        dependsOn 'copyJarsToModuleDeps'
    } else {
        dependsOn 'copyAppimageFiles'
    }
    dependsOn 'copyAppimageModules'
    workingDir buildDir

    if (moduleInfoPresent) {
        commandLine binjlink,
                '-J-Djlink.debug=true',
                '--module-path', "${javafxSdkLibDir}" + File.pathSeparator + "${buildDir}/appimage-modules",
                '--add-modules', ("br.com.pinter.tqdatabase,${moduleName},javafx.base,javafx.fxml,javafx.graphics,javafx.controls,java.prefs,java.base,jdk.zipfs,jdk.unsupported,java.logging,java.net.http,jdk.crypto.cryptoki"),
                '--output', "${buildDir}/jre-image",
                '--compress', '2',
                '--no-header-files',
                '--no-man-pages',
                '--strip-native-commands'
    } else {
        commandLine binjlink,
                '--module-path', "${javafxSdkLibDir}" + File.pathSeparator + "${buildDir}/appimage-modules",
                '--add-modules', ("javafx.base,javafx.fxml,javafx.graphics,javafx.controls,java.prefs,java.base,jdk.zipfs,jdk.unsupported,java.logging,java.net.http,jdk.crypto.cryptoki"),
                '--output', "${buildDir}/jre-image",
                '--compress', '2',
                '--no-header-files',
                '--no-man-pages',
                '--strip-native-commands'
    }
}

task archiveAppimage(type: Zip) {
    dependsOn 'appimage'
    doLast {
        println("Creating archive '${appArchiveName}'")
    }
    from "${buildDir}/app-image"
    archiveFileName = appArchiveName
    destinationDirectory = file("$buildDir")
}

task appimage(type: Exec) {
    dependsOn 'jlink'
    workingDir buildDir
    if (moduleInfoPresent) {
        commandLine binjpackage,
                '--type', 'app-image',
                '--verbose',
                '--runtime-image', "${buildDir}/jre-image",
                '--module', "${moduleName}/${mainClassName}",
                '--dest', "${buildDir}/app-image",
                '--name', distAppName,
                '--icon', "${projectDir}/src/main/resources/icon/icon64.ico",
                '--app-version', project.version,
                '--vendor', 'Emerson Pinter',
                '--input', "${buildDir}/appimage-files",
                '--description', 'The respec tool for Titan Quest game',
                '--java-options',
                '-XX:+UseSerialGC ' +
                        '-Dguice_bytecode_gen_option=DISABLED ' +
                        '-XX:MinHeapFreeRatio=10 -XX:MaxHeapFreeRatio=15 ' +
                        "--add-opens javafx.base/com.sun.javafx.reflect=ALL-UNNAMED " +
                        "--add-opens ${moduleName}/${mainPackageName}=ALL-UNNAMED " +
                        "--add-opens ${moduleName}/${mainPackageName}.core=ALL-UNNAMED " +
                        "--add-opens ${moduleName}/${mainPackageName}.tqdata=ALL-UNNAMED " +
                        "--add-opens ${moduleName}/${mainPackageName}.gui=ALL-UNNAMED " +
                        "--add-opens ${moduleName}/${mainPackageName}.save.player=ALL-UNNAMED " +
                        "--add-opens ${moduleName}/${mainPackageName}.save.stash=ALL-UNNAMED " +
                        "--add-opens ${moduleName}/${mainPackageName}.save=ALL-UNNAMED " +
                        "--add-opens java.base/java.lang=ALL-UNNAMED " +
                        "--add-reads ${moduleName}=ALL-UNNAMED"
    } else {
        commandLine binjpackage,
                '--type', 'app-image',
                '--verbose',
                '--runtime-image', "${buildDir}/jre-image",
                '--dest', "${buildDir}/app-image",
                '--name', distAppName,
                '--icon', "${projectDir}/src/main/resources/icon/icon64.ico",
                '--app-version', project.version,
                '--vendor', 'Emerson Pinter',
                '--input', "${buildDir}/appimage-files",
                '--description', 'The respec tool for Titan Quest game',
                '--main-jar', jar.outputs.files.collect { it.getName() }.get(0),
                '--main-class', mainClassName,
                '--java-options',
                '-XX:+UseSerialGC ' +
                        '-Dguice_bytecode_gen_option=DISABLED ' +
                        '-XX:MinHeapFreeRatio=10 -XX:MaxHeapFreeRatio=15 ' +
                        '--add-opens javafx.base/com.sun.javafx.reflect=ALL-UNNAMED ' +
                        '--add-opens java.base/java.lang=ALL-UNNAMED '
    }
    doLast {
        copy {
            if (OS.isWindows()) {
                from javafxSdkBinDir.listFiles().findAll {
                    it.getName().equalsIgnoreCase("decora_sse.dll") ||
                            it.getName().equalsIgnoreCase("fxplugins.dll") ||
                            it.getName().equalsIgnoreCase("glass.dll") ||
                            it.getName().equalsIgnoreCase("javafx_font.dll") ||
                            it.getName().equalsIgnoreCase("javafx_iio.dll") ||
                            it.getName().equalsIgnoreCase("prism_common.dll") ||
                            it.getName().equalsIgnoreCase("prism_d3d.dll") ||
                            it.getName().equalsIgnoreCase("prism_sw.dll")
                }.collect()
                into "$buildDir/app-image/${distAppName}/runtime/bin"
            } else {
                from javafxSdkLibDir.listFiles().findAll { it.getName().toLowerCase().endsWith('.so') }.collect()
                into "$buildDir/app-image/${distAppName}"
            }
        }
        new File(buildDir, "app-image/${distAppName}").listFiles().findAll {
            it.getName().equalsIgnoreCase("jfxwebkit.dll") ||
            it.getName().equalsIgnoreCase("ucrtbase.dll") ||
            it.getName().equalsIgnoreCase("gstreamer-lite.dll") ||
            it.getName().equalsIgnoreCase("glib-lite.dll") ||
            it.getName().toLowerCase().startsWith("api-ms-win-core") ||
            it.getName().toLowerCase().startsWith("api-ms-win-crt")
        }.forEach({
            f -> f.delete()
        })
        copy {
            from "${projectDir}/README.md"
            into new File(buildDir, "app-image/${distAppName}")
        }
        new File(buildDir, "app-image/${distAppName}/savedata").mkdir()
        new File(buildDir, "app-image/${distAppName}/gamedata").mkdir()
        new File(buildDir, "jre-image").deleteDir()
    }
}

clean.dependsOn subprojects.collect { it.tasks.matching { it.name == 'clean' } }